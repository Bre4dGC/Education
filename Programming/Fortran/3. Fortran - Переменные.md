Переменные - это именованные ссылки на данные, хранящиеся в памяти. При задании переменных в Фортране необходимо также указывать тип данных. Это означает, что Fortran является сильно типизированным языком по сравнению с Python, где типы данных переменных могут меняться во время выполнения кода. Python - динамически типизированный язык. По умолчанию в Fortran считается, что переменные, начинающиеся с букв от I до N, являются целыми числами, все остальные переменные считаются переменными с плавающей точкой (real). Это также называется правилом имплицитного типа и считается плохой практикой в Fortran, но используется по умолчанию, потому что многие программы на Fortran все еще полагаются на это правило. Современное приложение Fortran должно всегда отключать правило имплицитного типа, добавляя следующее утверждение в каждую единицу кода:
```fortran
implicit none
```
Более подробно это описано в следующих разделах.
### Именование переменных
Переменные в современном Фортране состоят из 1-31 алфавитно-цифровых символов (буквы, кроме и , подчеркивания и цифр). Первый символ имени переменной должен быть буквой. Допустимыми именами переменных могут быть:
```fortran
a
a_thing
x1
mass
q123
time_of_flight
```
Имена переменных могут состоять как из прописных, так и из строчных букв. Следует отметить, что `a` и `A` ссылаются на одну и ту же переменную. Недопустимыми именами переменных могут быть:
```fortran
1a      ! Первый символ не буква
a thing ! Содержит символ пробела
%       ! Содержит не алфавитно-нумерной символ
```
### Типы данных и объявления
В Фортране существует 5 встроенных типов данных:
```fortran
integer   ! Целые числа.
real      ! Числа с плавающей запятой.
complex   ! Комплексные числа.
logical   ! Булевы значения.
character ! Строки и символы.
```

Синтаксис объявления переменной следующий:
```fortran
type [[,attribute]... ::] entity-list
```
* **type** определяет тип переменной и может быть **integer**, **real**, **complex**, **logical**, **character** или **type( type-name )**. 
* **attribute** определяет дополнительные специальные атрибуты или способ использования переменной. В следующих примерах показаны некоторые типичные объявления переменных в Фортране.
```fortran
integer :: a     ! Скалярная целочисленная переменная
real    :: b     ! Скалярная переменная с плавающей точкой
logical :: flag  ! Логическая переменная

real :: D(10)    ! Массив с плавающей точкой, состоящий из 10 элементов
real :: K(20,20) ! Массив с плавающей точкой из 20x20 элементов

integer, dimension(10) :: C     ! Целочисленный массив из 10 элементов

character :: ch                 ! Символ
character, dimension(60) :: chv ! Массив символов
character(len=80) :: line       ! Строка из символов
character(len=80) :: lines(60)  ! Массив строк
```
Константы объявляются с помощью дополнительного атрибута **parameter**. Объявленная константа может быть использована в следующих объявлениях переменных. Пример использования показан в следующем примере.
```fortran
integer, parameter :: A = 5 ! Целочисленная константа
real :: C(A)                ! Массив с плавающей точкой, где
                            ! количество элементов
                            ! задается A
```

Точность и размер типа переменной можно указать, добавив круглую скобку непосредственно после объявления типа. Переменные **A** и **B** в следующем примере объявлены как скаляры с плавающей точкой с разной точностью. Число в круглой скобке означает для многих архитектур, сколько байт занимает переменная с плавающей точкой. Однако это не является стандартом, и на это не следует полагаться.
```fortran
real(8) :: A
real(4) :: B
integer(4) :: I
```
Чтобы иметь возможность выбрать правильную точность для переменной с плавающей точкой, в Fortran есть встроенная функция **selected_real_kind**, которая возвращает значение, которое будет использоваться в объявлении с заданной точностью. Это показано в следующем примере.
```fortran
integer, parameter :: dp = selected_real_kind(15,300)
real(kind=dp) :: X,Y
```
В данном примере переменная с плавающей точкой должна иметь не менее 15 значащих десятичных знаков и может представлять числа от 10<sup>-300</sup> до 10<sup>300</sup>. Для нескольких распространенных архитектур **selected_real_kind** вернет значение 8. Преимущество использования вышеописанного подхода заключается в том, что точность значений с плавающей точкой может быть задана независимым от архитектуры способом. Константа точности также может использоваться при указании чисел в присваиваниях переменных, как показано в следующем примере.
```fortran
X = 6.0_dp
```
Важность указания точности присвоения скалярных значений переменным иллюстрирует следующий пример.
```fortran
program constants
	
    implicit none
	
    integer, parameter :: dp = selected_real_kind(15,300)
	
    real(dp) :: pi1, pi2
    pi1 = 3.141592653589793
    pi2 = 3.141592653589793_dp
	
    write(*,*) 'pi1 = ', pi1
    write(*,*) 'pi2 = ', pi2
	
    stop
	
end program constants
```

Программа выдает следующие результаты:
```fortran
pi1 = 3.14159274101257
pi2 = 3.14159265358979
```
Скалярное число, присвоенное переменной **pi1**, выбирается компилятором для представления с точностью до плавающей точки с наименьшим количеством байт, в данном случае **real(4)**, что и показано на выходе из приведенной выше программы.

Объявление переменных в Фортране всегда предшествует исполняемым операторам в основной программе или подпрограмме. В модулях объявления также могут располагаться непосредственно после идентификатора **модуля**.
### Правило неявного типа
Переменные не обязательно должны быть объявлены в Фортране. По умолчанию переменные, начинающиеся с I, J,..., N, определяются как **integer**, а переменные, начинающиеся с A, B,...,H или O, P,...,Z, определяются как **real**. Такое неявное объявление переменных не рекомендуется, поскольку оно может привести к ошибкам в программировании, когда переменные будут неправильно написаны. Чтобы избежать неявного объявления переменных, в программе или модуле на первом месте можно разместить следующее объявление:
```fortran
implicit none
```
Этот оператор заставляет компилятор убедиться, что все переменные объявлены. Если переменная не объявлена, компиляция будет остановлена с сообщением об ошибке. Это стандартное значение для многих других сильно типизированных языков, таких как C, C++ и Java.
### Назначение переменных
Синтаксис присвоения скалярной переменной следующий:
```fortran
variable = expr
```
где variable обозначает переменную, которую нужно присвоить, а expr - выражение, которое нужно присвоить. В следующем примере переменной a присваивается значение 5.0 с точностью, заданной в константе dp.
```fortran
a = 5.0_dp
```
Присваивание булевых переменных выполняется аналогичным образом с использованием ключевых слов .false. и .true., обозначающих истинное или ложное значение. В присваивании также может использоваться булево выражение. В следующем примере переменной flag присвоено значение .false.
```fortran
flag =.false.
```

Назначение строк показано в следующем примере.
```fortran
character(40) :: first_name
character(40) :: last_name
character(20) :: company_name1
character(20) :: company_name2

...

first_name = 'Jan'
last_name = "Johansson"
company_name1 = "McDonald's"
company_name2 = 'McDonald''s'
```
Первой переменной, first_name, присваивается текст 'Jan', остальные символы в строке будут заполнены пробелами. Строка присваивается с помощью знаков цитирования '' или апострофов ''. Это может быть полезно, когда апострофы или знаки цитирования используются в строках, как показано в назначении переменных company_name1 och company_name2.
### Определенные и неопределенные переменные
Переменная в Fortran, которой присвоено значение, считается определенной и может безопасно использоваться. Переменные, которым не присвоено значение, считаются неопределенными и не должны использоваться.

Программа, содержащая неопределенные переменные, не завершится компиляцией. Память для неопределенных переменных будет зарезервирована, и на нее можно будет ссылаться. Однако значения неопределенных переменных не устанавливаются автоматически в ноль и ссылаются на участки памяти с неизвестными значениями. Часто такие переменные возвращают мусорные или случайные значения. Как правило, всегда инициализируйте переменные значениями по умолчанию или убедитесь, что им присвоено значение из другой ссылки на переменную.

В следующем примере показан пример обращения к определенным и неопределенным переменным.
```fortran
program undef1
	
    implicit none
	
    integer, parameter :: dp = selected_real_kind(15,300)
	
    real(dp) :: a, b
    character(40) :: s1, s2
	
    a = 42.0_dp ! --- Определенный
    s1 = 'My defined string'
	
    print*, a
    print*, b
    print*, s1
    print*, s2
	
end program undef1
```

В результате будет выведено следующее:
```
42.000000000000000
8.2890460584580950E-317
My defined string
```
Причина значения 8.28...E-317 заключается в том, что ссылка на переменную **b** указывает на заданную область памяти, но этому месту не было присвоено никакого значения, и оно будет содержать все, что было в памяти на момент выполнения программы. Фортран интерпретирует значения в этом месте как значения с плавающей точкой и представит их в таком виде. Это иллюстрирует, почему лучше инициализировать переменные значением 0.0_dp или убедиться, что им будут присвоены значения из других ссылок на переменные.
### Производные типы данных
В некоторых случаях может быть полезно создать собственные типы данных для обработки поведения вашей программы. Для создания новых типов данных в Fortran вы можете создавать производные типы данных с помощью оператора **type**. С помощью этого оператора можно создать новый тип данных, сгруппировав существующие типы данных Fortran в новый тип. В следующем примере задан тип данных частица:
```fortran
type particle
    real(dp) :: x
    real(dp) :: y
    real(dp) :: z
    real(dp) :: m
end type particle
```

Теперь новый тип данных можно использовать как любой другой тип данных в Фортране. Для создания ссылки в переменной на производный тип данных ключевое слово **type** предшествует имени типа данных в объявлении. Как в следующем примере:
```fortran
type(particle) :: p0
```

Доступ к членам производных типов данных осуществляется с помощью **%**-оператора. В следующем примере членам переменной **p0** присваиваются значения:
```fortran
p0 % x = 0.0_dp
p0 % y = 0.0_dp
p0 % z = 0.0_dp
p0 % m = 1.0_dp
```

Производные типы данных могут содержать несколько типов данных Fortran:
```fortran
type particle
    real(dp)  :: x
    real(dp)  :: y
    real(dp)  :: z
    real(dp)  :: m
    logical   :: active
    integer   :: id
    character :: name(8)
end type particle
```
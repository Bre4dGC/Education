В Фортране есть множество встроенных функций, охватывающих различные области. В следующих таблицах приведены некоторые из них. Более подробное описание встроенной функции можно найти в книге Metcalf and Reid [metcalfreid18](https://modern-fortran-in-science-and-technology.readthedocs.io/en/latest/references.html#metcalfreid18).
### Математические функции

| Функция    | Описание                                                               |
| ---------- | ---------------------------------------------------------------------- |
| acos(x)    | Возвращает $\arccos(x)$                                                |
| asin(x)    | Возвращает $\arcsin(x)$                                                |
| atan(x)    | Возвращает $\arctan(x)$                                                |
| atan2(y,x) | Возвращает $\arctan\left( \frac{y}{x} \right) from -\pi$ till math:-pi |
| cos(x)     | Возвращает $\cos(x)$                                                   |
| cosh(x)    | Возвращает $\cosh(x)$                                                  |
| exp(x)     | Возвращает $e^{x}$                                                     |
| log(x)     | Возвращает $\ln(x)$                                                    |
| log10(x)   | Возвращает $\lg(x)$                                                    |
| sin(x)     | Возвращает $\sin(x)$                                                   |
| sinh(x)    | Возвращает $\sinh(x)$                                                  |
| sqrt(x)    | Возвращает $\sqrt{x}$                                                  |
| tan(x)     | Возвращает $\tan(x)$                                                   |
| tanh(x)    | Возвращает $\tanh(x)$                                                  |
### Различные функции преобразования

| Function          | Description                                                      |
| ----------------- | ---------------------------------------------------------------- |
| abs(a)            | Возвращает абсолютное значение                                   |
| aint(a)           | Усекает значение с плавающей точкой                              |
| int(a)            | Преобразование значения с плавающей точкой в целое число         |
| nint(a)           | Округляет значение с плавающей точкой до ближайшего целого числа |
| real(a)           | Преобразование целого числа в значение с плавающей точкой        |
| max(a1,a2[,a3,…]) | Возвращает максимальное значение из двух или более значений      |
| min(a1,a2[,a3,…]) | Возвращает минимальное значение из двух или более значений       |

### Векторные и матричные функции

| Function          | Description                                                                      |
| ----------------- | -------------------------------------------------------------------------------- |
| dot_product(u, v) | Возвращает скалярное произведение $u - v$.                                       |
| matmul(A, B)      | Умножение матриц. Результат должен быть таким же, как $AB$                       |
| transpose(C)      | Возвращает транспонирование $C^{T}$. Элемент $C^{T}_{ij}$ соответствует $C_{ji}$ |

### Функции массива

| Функция        | Описание                                                                                                                                      |
| -------------- | --------------------------------------------------------------------------------------------------------------------------------------------- |
| all(mask)      | Возвращает true, если все элементы логического массива **mask** истинны. Например, all(A>0)! возвращает true, если все элементы в A больше 0. |
| any(mask)      | Возвращает true, если любой из элементов в **маске** является true.                                                                           |
| count(mask)    | Возвращает количество элементов в **маске**, которые являются истинными.                                                                      |
| maxval(array)  | Возвращает максимальное значение элементов в массиве **array**.                                                                               |
| minval(array)  | Возвращает минимальное значение элементов в массиве **array**.                                                                                |
| product(array) | Возвращает произведение элементов массива **array**.                                                                                          |
| sum(array)!    | Возвращает сумму элементов в массиве **array**.                                                                                               |
Большинство встроенных функций и операторов в Fortran поддерживают массивы. В следующем примере показано, как функции и операторы поддерживают операции над массивами.
```fortran
real, dimension(20,20) :: A, B, C
C = A / B   ! Деление
C = sqrt(A) ! Квадратный корень
```
Деление $C_{ij}=\frac {A_{ij}}{B_{ij}}$
Квадратный корень $C_{ij}=\sqrt{A_{ij}}$

В следующем примере показано, как с помощью этих функций и операторов можно легко создать матрицу жесткости для балочного элемента. Матрица $K_{e}$ определяется следующим образом:

$$
K_{e} = (G^TK_{el})G
$$

Возврат $G^T$ осуществляется с помощью фортрановской функции **transpose**, а умножение матриц - с помощью **matmul**. Матрицы $K_{el}$ и $G$ определяются как:
$$
K_{el} = \frac{EA}{L}[\frac{1\;\;-1}{-1\;\;1}]
$$

и:
$$
G= [\frac{n_{x}\;n_{y}\;n_{z}\;0\;0\;0}{0\;0\;0\;n_{x}\;n_{y}\;n_{z}}]
$$

Косинусы длины и направления определяются как:
$$
L=\sqrt{ (x_{2}-x_{1})^2+(y_{2}-y_{1})^2+(z_{2}-z_{1})^2 }
$$
$$
n_{x}=\frac{x_{2}-x_{1}}{L},n_{y}=\frac{y_{2}-y_{1}}{L},n_{z}=\frac{z_{2}-z_{1}}{L}
$$

В примере входным параметрам присвоены следующие значения:
$x_{1}=0,y_{1}=0,z_{1}=0$
$x_{2}=1,y_{2}=1,z_{2}=1$
$E=1,A=1$

```fortran
program function_sample
	
    implicit none
	
    integer, parameter :: dp = selected_real_kind(15,300)
	
    integer :: i, j
	
    real(dp) :: x1, x2, y1, y2, z1, z2
    real(dp) :: nx, ny, nz
    real(dp) :: L, E, A
    real(dp) :: Kel(2,2)
    real(dp) :: Ke(6,6)
    real(dp) :: G(2,6)
	
    ! Инициируйте скалярные значения	
    E = 1.0_dp
    A = 1.0_dp
    x1 = 0.0_dp
    x2 = 1.0_dp
    y1 = 0.0_dp
    y2 = 1.0_dp
    z1 = 0.0_dp
    z2 = 1.0_dp
	
    ! Рассчитайте косинусы направлений	
    L = sqrt( (x2-x1)**2 + (y2-y1)**2 + (z2-z1)**2 )
    nx = (x2-x1)/L
    ny = (y2-y1)/L
    nz = (z2-z1)/L
	
    ! Вычислить локальную матрицу жесткости	
    Kel(1,:) = (/  1.0_dp , -1.0_dp  /)
    Kel(2,:) = (/ -1.0_dp,   1.0_dp  /)
	
    Kel = Kel * (E*A/L)
	
    G(1,:) = (/ nx, ny, nz, 0.0_dp, 0.0_dp, 0.0_dp /)
    G(2,:) = (/ 0.0_dp, 0.0_dp, 0.0_dp, nx, ny, nz /)
	
    ! Вычислите преобразованную матрицу жесткости	
    Ke = matmul(matmul(transpose(G),Kel),G)
	
    ! Печать матрицы	
    do i=1,6
        write(*,'(6G10.3)') (Ke(i,j), j=1,6)
    end do
	
    stop
	
end program function_sample
```

Программа выдает следующий результат
```
0.1925    0.1925    0.1925    -.1925    -.1925    -.1925
0.1925    0.1925    0.1925    -.1925    -.1925    -.1925
0.1925    0.1925    0.1925    -.1925    -.1925    -.1925
-.1925    -.1925    -.1925    0.1925    0.1925    0.1925
-.1925    -.1925    -.1925    0.1925    0.1925    0.1925
-.1925    -.1925    -.1925    0.1925    0.1925    0.1925
```
Более подробное описание работы с матрицами в Fortran 90/95
### Элементарные процедуры
### Векторные и матричные функции
### Процедуры сокращения
### Информационные функции